apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion project.rootProject.android.compile_sdk_version

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def BUGLY_APPID = properties.getProperty('bugly.appid')
    def LEANCLOUD_APPID = properties.getProperty('leancloud.appid')
    def LEANCLOUD_APPKEY = properties.getProperty('leancloud.appkey')

    defaultConfig {
        minSdkVersion project.rootProject.android.min_sdk_version
        targetSdkVersion project.rootProject.android.target_sdk_version
        versionCode version_code
        versionName version_name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [VERSION_NAME    : defaultConfig.versionName,
                                DEBUGGABLE      : false,
                                APP_CHANNEL     : "dev",
                                BUGLY_APPID     : BUGLY_APPID,
                                LEANCLOUD_APPID : LEANCLOUD_APPID,
                                LEANCLOUD_APPKEY: LEANCLOUD_APPKEY]

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'armeabi-v7a'//, 'x86', 'x86_64', 'arm64-v8a'
        }
    }

    // LEANCLOUD BEGIN
    //为了解决部分第三方库重复打包了META-INF的问题
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }
    // LEANCLOUD END

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [DEBUGGABLE: debuggable]
        }

        debug {
            debuggable true
            manifestPlaceholders = [DEBUGGABLE: debuggable]
            ndk {
                //设置支持的SO库架构
                abiFilters 'armeabi', 'armeabi-v7a', 'x86', 'x86_64', 'arm64-v8a'
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$android_support_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    //注释掉原有bugly的仓库
    //implementation 'com.tencent.bugly:crashreport:2.6.6.1'
    //implementation 'com.tencent.bugly:crashreport:latest.release'
    api 'com.tencent.bugly:crashreport_upgrade:1.3.4'
    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'

    // LeanCloud 基础包
    implementation 'cn.leancloud.android:avoscloud-sdk:v4.4.4'
    // LeanCloud 统计包
    implementation 'cn.leancloud.android:avoscloud-statistics:v4.4.4'
    // LeanCloud 用户反馈包
    api 'cn.leancloud.android:avoscloud-feedback:v4.4.4@aar'
    // 推送与实时聊天需要的包
    implementation('cn.leancloud.android:avoscloud-push:v4.4.4@aar') { transitive = true }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
